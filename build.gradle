buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}


apply plugin: "curseforge"
apply plugin: "forge"
apply plugin: 'maven-publish'

def build_number = 'CUSTOM'

if (System.getenv('BUILD_NUMBER') != null)
    build_number = System.getenv('BUILD_NUMBER')

version = "${mc_version}-${mod_version}"
group= package_group
archivesBaseName = mod_name

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"
	
    if (project.hasProperty('mappings_version'))

        mappings = project.mappings_version

}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
	ivy {
		name "CoFHCore"
        artifactPattern "http://addons-origin.cursecdn.com/files/2229/440/[module]-[revision].[ext]"
    }
	
	ivy {
		name "MineTweaker3"
        artifactPattern "http://minetweaker3.powerofbytes.com/download/[module]-[revision].[ext]"
    }
	
	maven {
		name = "ic2"
		url = "http://maven.ic2.player.to/"
	}
}

dependencies {
    compile name: 'CoFHCore', version: "${cofhcore_version}", ext: 'jar'
    compile name: 'MineTweaker3', version: "${minetweaker_version}", ext: 'jar'
	compile 'net.industrial-craft:industrialcraft-2:${ic2_version}-experimental:api'
}

jar {
    classifier = ''
}

task 'v++' <<{
	Properties props = new Properties() 
	File propsFile = new File('gradle.properties') 
	
	props.load(propsFile.newDataInputStream()) 
	
	String nextbuildnum = props.getProperty('mod_version')
	int index = nextbuildnum.lastIndexOf('.')
	props.setProperty('mod_version', nextbuildnum.substring(0, index + 1) + ((nextbuildnum.substring(index + 1) as Integer) + 1).toString())

	props.store(propsFile.newWriter(), null) 
	props.load(propsFile.newDataInputStream())
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

tasks.build.dependsOn sourceJar, deobfJar

tasks.withType(JavaCompile) { task ->
    task.options.encoding = 'UTF-8'
}


curse {
    if (project.hasProperty('curse_key')) {
	apiKey = project.curse_key
    }    
    projectId = "224011"
    changelog = project.hasProperty('changelog') ? project.changelog : ''
    releaseType = "release"
    relatedProject 'cofhcore': 'requiredLibrary'
    
    additionalArtifact deobfJar
    
}

publishing {
    tasks.publish.dependsOn 'build'
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourceJar
            artifact javadocJar
            artifact deobfJar
        }
    }
	
    repositories {
        if (project.hasProperty('maven_repo')) {
            maven { url maven_repo }
        } else {
            mavenLocal()
        }
    }
}
